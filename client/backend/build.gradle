// Copyright (c) Microsoft. All rights reserved.
// Licensed under the MIT license. See License.txt in the project root.

plugins {
    id "application"
    id "org.jetbrains.kotlin.jvm"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation project("tfs-sdk")
    implementation "com.jetbrains.rd:rd-framework:$rdGenVersion"
    
    // Test dependencies - needed because test files are in main sourceset
    implementation 'junit:junit:4.12'
}

compileKotlin {
    // Note: protocol dependency disabled due to rdgen compatibility issues  
    // Generated files are manually maintained
    // dependsOn ':client:protocol:rdgen'
    kotlinOptions.jvmTarget = '21'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '21'
}

application {
    mainClass = "com.microsoft.tfs.MainKt"
}

evaluationDependsOnChildren() // to get properties of tfs-sdk subproject

task docs(type: Copy, dependsOn: ["tfs-sdk:downloadArchive", "tfs-sdk:verifyArchive"]) {
    def docs = file("$buildDir/docs")

    def tfsSdk = project("tfs-sdk")
    def sdkVersion = tfsSdk.ext.sdkVersion
    def sdkArchiveLocation = tfsSdk.ext.sdkArchiveLocation
    from(zipTree(sdkArchiveLocation)) {
        include "TFS-SDK-$sdkVersion/*.html"
        include "TFS-SDK-$sdkVersion/redist/*.*"
        eachFile { fcd ->
            fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(fcd.relativePath.segments.size() - 1))
        }
        includeEmptyDirs = false
    }
    into docs
}

test.onlyIf { "true".equalsIgnoreCase(System.getenv("MSVSTS_INTELLIJ_RUN_L2_TESTS")) }

distributions {
    main {
        contents {
            from(docs)
        }
    }
}
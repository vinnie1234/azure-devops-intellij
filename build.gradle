/**
 * This is the main gradle build file for the Azure DevOps IntelliJ Plugin.
 */
buildscript {
    ext {
        // https://search.maven.org/artifact/com.jetbrains.rd/rd-gen
        rdGenVersion = '2021.3.0'
    }

    dependencies { classpath "com.jetbrains.rd:rd-gen:$rdGenVersion" }
}

plugins {
    id 'org.jetbrains.intellij.platform' version '2.7.2' apply false
    // https://docs.gradle.org/current/userguide/compatibility.html#kotlin
    id 'org.jetbrains.kotlin.jvm' version '1.9.24' apply false
    id "de.undercouch.download" version "4.1.2"
}

import org.jetbrains.intellij.platform.gradle.TestFrameworkType

/**
 * This is task for update Gradle wrapper version.
 */
wrapper {
    gradleVersion = '8.10'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

apply from: 'dependencies.gradle'

def mainProjects = [project(":plugin"), project(':plugin:test-utils')]
def l2TestsProject = project(":L2Tests")
def clientProjects = [project(":client:backend"), project(":client:connector")]

/**
 * settings common to ALL projects (even the root level project)
 */
allprojects {
    repositories {
        flatDir {
            dirs externalDependenciesDirectory
        }
        mavenCentral()
        maven {
            url "https://cache-redirector.jetbrains.com/www.myget.org/F/rd-snapshots/maven"
        }
    }
}

/**
 * settings shared by the main projects (part 1)
 */
configure(mainProjects + clientProjects) {
    configurations {
        // Some our dependencies should be included *before* dependencies from IDEA (e.g. junit, jackson-databind). IDEA
        // has some versions of these dependencies that sometimes break tests (see comments for particular dependencies
        // included into this configuration for details).
        priorityTestImplementation
    }

    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }

            resources {
                srcDir 'resources'
            }
        }

        test {
            def setUpDependencies = { originalCollection ->
                configurations.priorityTestImplementation + originalCollection
            }

            compileClasspath = setUpDependencies(compileClasspath)
            runtimeClasspath = setUpDependencies(runtimeClasspath)

            java {
                srcDir 'test'
            }
            resources {
                srcDir 'test-resources'
            }
        }
    }

    test {
        forkEvery = 10
    }
}

def reactiveBackend = file("client/backend/build/install")

/**
 * specific settings for each project
 */
project(":plugin") {
    apply plugin: 'org.jetbrains.intellij.platform'

    repositories {
        intellijPlatform {
            defaultRepositories()
        }
    }

    dependencies {
        implementation project(path: ":client:connector", configuration: "default")

        testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.2'
        testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.2'
        testImplementation group: 'org.powermock', name: 'powermock-classloading-xstream', version: '2.0.2'

        testRuntimeOnly project(":plugin:test-utils")

        // IntelliJ Platform dependencies
        intellijPlatform {
            intellijIdeaCommunity(ideaVersion)
            bundledPlugin('Git4Idea')
            bundledPlugin('com.intellij.java')
            
            pluginVerifier()
            zipSigner()
            instrumentationTools()
        }
    }

    version buildNumber

    intellijPlatform {
        buildSearchableOptions = false
        
        pluginConfiguration {
            name = 'com.microsoft.vso.idea'
        }

        publishing {
            token = providers.environmentVariable("PUBLISH_TOKEN")
        }

        pluginVerification {
            ides {
                recommended()
            }
        }

        // Custom sandbox tasks for reactive backend
        tasks.register('prepareBackendSandbox', Copy) {
            dependsOn ":client:backend:installDist"
            from reactiveBackend
            into layout.buildDirectory.dir("idea-sandbox/plugins/${project.name}")
        }
        
        tasks.named('prepareSandbox') {
            finalizedBy('prepareBackendSandbox')
        }
    }

    jar {
        archiveBaseName = 'com.microsoft.alm.plugin.idea'
    }

    task showVersion() {
        println("version = " + buildNumber)
        println("ideaVersion = " + ideaVersion)
    }

    task zip(dependsOn: ['buildPlugin','test']) {}

    // Uncomment for IDE internal mode:
    //runIde {
    //    jvmArgs("-Didea.is.intenal=true")
    //}
}

project(':plugin:test-utils') {
    apply plugin: 'org.jetbrains.intellij.platform'

    repositories {
        intellijPlatform {
            defaultRepositories()
        }
    }

    dependencies {
        implementation project(':plugin')

        // IntelliJ Platform dependencies
        intellijPlatform {
            intellijIdeaCommunity(ideaVersion)
            
            instrumentationTools()
        }
    }

    intellijPlatform {
        buildSearchableOptions = false
    }
}

project(":L2Tests") {
    // L2Tests temporarily disabled due to IntelliJ 2025.2 test framework compatibility issues
    // These tests use deprecated test framework APIs that need significant refactoring
    
    apply plugin: 'java'
    
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }

    dependencies {
        implementation project(':plugin')
    }

    jar {
        archiveBaseName = 'com.microsoft.alm.L2'
    }

    // Disable compilation of test classes until they are migrated to new test framework APIs
    tasks.named('compileTestJava') {
        enabled = false
    }
    
    tasks.named('test') {
        enabled = false
    }
}

/**
 * settings shared by the main projects (part 2)
 */
configure(mainProjects) {
    dependencies {
        // Dependencies of ':com.microsoft.alm.client:0.4.3-SNAPSHOT'
        implementation 'javax.ws.rs:javax.ws.rs-api:2.0.1'
        implementation 'commons-codec:commons-codec:1.10'
        implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.4.1'

        // Microsoft ALM libraries that are managed by the dependencies.gradle script
        implementation ':com.microsoft.alm.client:0.4.3-SNAPSHOT'
        implementation ':com.microsoft.alm.client.build2:0.4.3-SNAPSHOT'
        implementation ':com.microsoft.alm.client.core:0.4.3-SNAPSHOT'
        implementation ':com.microsoft.alm.client.distributedtask:0.4.3-SNAPSHOT'
        implementation ':com.microsoft.alm.client.sourcecontrol:0.4.3-SNAPSHOT'
        implementation ':com.microsoft.alm.client.workitemtracking:0.4.3-SNAPSHOT'

        implementation group: 'commons-io', name: 'commons-io', version: '2.4'
        implementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '2.28'
        implementation group: 'org.glassfish.jersey.connectors', name: 'jersey-apache-connector', version: '2.28'
        implementation group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.28'
        implementation (group: 'org.apache.httpcomponents', name: 'httpclient-win', version: '4.4.1') {
            exclude group: 'net.java.dev.jna', module: 'jna'
            exclude group: 'net.java.dev.jna', module: 'jna-platform'
        }

        implementation (group: 'com.microsoft.alm', name: 'auth-core', version: '0.6.4') {
            exclude group: 'org.slf4j', module: 'slf4j-api'
        }

        // IDEA has two other junit versions embedded into it: 3.8.1 (junit.jar) and 4.12 (junit-4.12.jar). We need our
        // version to take priority.
        priorityTestImplementation group: 'junit', name: 'junit', version: '4.12'

        testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
        testImplementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    /**
     * Open source prep
     */
    checkstyle {
        toolVersion = "6.1"
        ignoreFailures = false
        configFile = file "${rootDir}/config/checkstyle/custom-rules.xml"
        configProperties = [
            'checkstyle.java.header': "${rootDir}/config/checkstyle/java.header"
        ]
    }

    pmd {
        toolVersion = "5.0.3"
        ignoreFailures = true
        ruleSetFiles = files "${rootDir}/config/pmd/custom-pmd-rules.xml"
    }
}
